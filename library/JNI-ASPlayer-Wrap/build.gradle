plugins {
    id 'com.android.library'
}

// 是否调用板卡中的libjniasplayer-jni.so
def useSystemJniASPlayer = USE_SYSTEM_JNI_ASPLAYER.toBoolean() ? 1 : 0

// JNI-ASPlayer settings
def jniAsPlayerDir = gradle.ext.JniASPlayerLibraryDir
//def jniAsPlayerDir = "/../../libs/JNI-ASPlayer-library"
def jniAsPlayerIncludeDir = jniAsPlayerDir + "/build/sdk/include"
// link so with symbols
def jniAsPlayerSoDir = jniAsPlayerDir + "/build/symbols"

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    ndkVersion rootProject.ext.android.ndkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.build.versionCode
        versionName rootProject.ext.build.versionName

        externalNativeBuild {
            cmake {
                arguments "-DUSE_SYSTEM_SO=${useSystemJniASPlayer}",
                        "-DJNI_ASPLAYER_INCLUDE_DIR=${jniAsPlayerIncludeDir}",
                        "-DJNI_ASPLAYER_SO_DIR=${jniAsPlayerSoDir}"
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            jni {
                srcDirs 'src\\main\\jni'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libjniasplayer-jni.so'
        pickFirst 'lib/arm64-v8a/libjniasplayer-jni.so'
    }
}

dependencies {
    compileOnly files('sdk/tuner_framework.jar')

    api project(":JNI-ASPlayer-library")

    testImplementation rootProject.ext.testDep.junit
    androidTestImplementation rootProject.ext.testDep.androidJunit
    androidTestImplementation rootProject.ext.testDep.espresso
}